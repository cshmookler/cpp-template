# Declare project information
project(
    meson.get_external_property('_name', 'unknown'),
    'cpp',
    version : meson.get_external_property('_version', 'unknown'),
    default_options : [ 'cpp_std=c++11' ],
)

# Get project name, version, and binary data
project_name = meson.project_name()
if project_name == 'unknown'
    error('Failed to get project name from Conan')
endif

project_version = meson.project_version()
if project_version == 'unknown'
    error('Failed to get project version from Conan')
endif

project_binaries = meson.get_external_property('_binaries', [ 'unknown' ])
if project_binaries == [ 'unknown' ]
    error('Failed to get project binary data from Conan')
endif

# Project root directory
root_dir = meson.project_source_root()

# Insert the project version into the version header file
conf_data = configuration_data()
conf_data.set('version', project_version)
configure_file(
    configuration : conf_data,
    input : '___version_header_dir___' / 'version.hpp.in',
    output : 'version.hpp',
)

# Copy the generated version header file to the source directory
pymod = import('python')
python = pymod.find_installation('python3')
run_command(
    python,
    root_dir / 'build_scripts' / 'copy.py',
    'version.hpp',
    '___version_header_dir___' / 'version.hpp',
    check : true,
)

foreach binary : project_binaries
    # Accumulate binary information
    binary_name = binary[0]
    binary_type = binary[1]
    binary_dependencies = binary[2]
    binary_components = []
    foreach dependency : binary_dependencies
        dependency_name = dependency[0]
        dependency_version = dependency[1]
        dependency_enabled = dependency[2]
        dependency_link_preference = dependency[3]
        dependency_dynamic = dependency[4]
        dependency_components = dependency[5]
        foreach component : dependency_components
            component_name = component[0]
            component_enabled = component[1]
            if dependency_link_preference
                binary_components += dependency(
                    component_name,
                    version : dependency_version,
                    static : not dependency_dynamic,
                    required : true,
                    method : 'auto',
                )
            else
                binary_components += dependency(
                    component_name,
                    version : dependency_version,
                    required : true,
                    method : 'auto',
                )
            endif
        endforeach
    endforeach

    # Accumulate the paths to the header files for the binary
    binary_headers = files()
    foreach header : binary[3]
        header_path = root_dir
        foreach header_segment : header
            header_path = header_path / header_segment
        endforeach
        binary_headers += header_path
    endforeach

    # Accumulate the paths to the source files for the binary
    binary_sources = files()
    foreach source : binary[4]
        source_path = root_dir
        foreach source_segment : source
            source_path = source_path / source_segment
        endforeach
        binary_sources += source_path
    endforeach

    # Accumulate the path to the main file for the binary
    binary_main = files()
    main_path = root_dir
    binary_has_main = binary[5].length() > 0
    foreach main_segment : binary[5]
        main_path = main_path / main_segment
    endforeach
    binary_main += main_path

    # Compile the binary
    if binary_type == 'application'
        if binary_has_main
            binary_sources += binary_main
        else
            # A 'main' function is required for applications
            warning(
                'Binary "' + binary_name + '" does not have an explicitly declared main function',
            )
        endif
        executable(
            binary_name,
            binary_sources,
            dependencies : binary_components,
            install : true,
        )
    elif binary_type == 'library'
        library(
            binary_name,
            binary_sources,
            dependencies : binary_components,
            version : project_version,
            install : true,
        )
        install_headers(binary_headers, subdir : binary_name)
    elif binary_type == 'test'
        if binary_has_main
            binary_sources += binary_main
        endif
        test = executable(
            binary_name,
            binary_sources,
            dependencies : binary_components,
        )
        test(binary_name, test)
    else
        error(
            'Invalid binary type "' + binary_type + '". Must either be "application", "library", or "test"',
        )
    endif
endforeach
